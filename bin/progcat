#!perl

our $DATE = '2014-11-28'; # DATE
our $VERSION = '0.14'; # VERSION

use 5.010;
use strict;
use warnings;

use App::ProgUtils;
use Perinci::CmdLine::Any -prefer_lite=>1;

our %SPEC;
$SPEC{progcat} = {
    v => 1.1,
    summary => 'Print command sources to stdout',
    description => <<'_',

Basically

    % progcat PROG1 PROG2 ...

is roughly the same as:

    % cat `which PROG1` `which PROG2` ...

except that it's written in Perl and offers (a case-insensitive) tab completion.

_
    args => {
        program => {
            schema => ['array*', of => 'str*', min_len=>1],
            req => 1,
            pos => 0,
            greedy => 1,
            element_completion => $App::ProgUtils::_complete_program,
        },
    },
};
sub progcat {
    my %args = @_;

    my $progs = $args{program};
    return [400, "Please specify at least one program"] unless @$progs;

    my $has_success;
    my $has_error;
    for my $prog (@{$progs}) {
        my $path = App::ProgUtils::_search_program($prog) or do {
            warn "progcat: No such program '$prog'\n";
            $has_error++;
            next;
        };
        open my $fh, "<", $path or do {
            warn "progcat: Can't open '$path': $!\n";
            $has_error++;
            next;
        };
        print while <$fh>;
        close $fh;
        $has_success++;
    }

    if ($has_error) {
        if ($has_success) {
            return [207, "Some programs failed"];
        } else {
            return [500, "All programs failed"];
        }
    } else {
        return [200, "All programs OK"];
    }
}

Perinci::CmdLine::Any->new(
    url => '/main/progcat',
)->run;

# ABSTRACT: Print command sources to stdout
# PODNAME: progcat

__END__

=pod

=encoding UTF-8

=head1 NAME

progcat - Print command sources to stdout

=head1 VERSION

This document describes version 0.14 of progcat (from Perl distribution App-ProgUtils), released on 2014-11-28.

=head1 SYNOPSIS

Basic usage:

 % progcat dzil

=head1 COMPLETION

This script has shell tab completion capability with support for several shells.

=head2 bash

To activate bash completion for this script, put:

 complete -C progcat progcat

in your bash startup (e.g. C<~/.bashrc>). Your next shell session will then recognize tab completion for the command. Or, you can also directly execute the line above in your shell to activate immediately.

You can also install L<App::BashCompletionProg> which makes it easy to add completion for Perinci::CmdLine-based scripts. After you install the module and put C<. ~/.bash-complete-prog> (or C<. /etc/bash-complete-prog>), you can just run C<bash-completion-prog> and the C<complete> command will be added to your C<~/.bash-completion-prog>. Your next shell session will then recognize tab completion for the command.

=head2 tcsh

To activate tcsh completion for this script, put:

 complete progcat 'p/*/`progcat`/'

in your tcsh startup (e.g. C<~/.tcshrc>). Your next shell session will then recognize tab completion for the command. Or, you can also directly execute the line above in your shell to activate immediately.

=head2 zsh

To activate zsh completion for this script, put:

 _progcat() { read -l; local cl="$REPLY"; read -ln; local cp="$REPLY"; reply=(`COMP_LINE="$cl" COMP_POINT="$cp" progcat`) }

 compctl -K _progcat progcat

in your zsh startup (e.g. C<~/.zshrc>). Your next shell session will then recognize tab completion for the command. Or, you can also directly execute the line above in your shell to activate immediately.

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-ProgUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/sharyanto/perl-App-ProgUtils>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-ProgUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2014 by perlancar@cpan.org.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
